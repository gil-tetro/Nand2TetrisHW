// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    /*  uses Dmux to determine which RAM to access 
        the input of the DMux is load and then, using the first 3 bits of the address,
        it decides which RAM to access.
    */
    DMux8Way(in=load, sel=address[0..2], a=ram0, b=ram1, c=ram2, d=ram3, e=ram4, f=ram5, g=ram6, h=ram7);

    /*  each ram receives the load from the DMux and the second half of the address to specify which
        register within it to access.
    */
    RAM8(in=in, load=ram0, address=address[3..5], out=out0);
    RAM8(in=in, load=ram1, address=address[3..5], out=out1);
    RAM8(in=in, load=ram2, address=address[3..5], out=out2);
    RAM8(in=in, load=ram3, address=address[3..5], out=out3);
    RAM8(in=in, load=ram4, address=address[3..5], out=out4);
    RAM8(in=in, load=ram5, address=address[3..5], out=out5);
    RAM8(in=in, load=ram6, address=address[3..5], out=out6);
    RAM8(in=in, load=ram7, address=address[3..5], out=out7);
    
    /* uses Mux to choose which RAM's state to output
       receives every RAM's state and chooses which one to output using the first 3 bits of the address  
    */
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[0..2], out=out);
}